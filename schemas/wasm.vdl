/*********************
* MAIN WASM FUNCTION *
*********************/

""" Represents a function to be called via WASM """
enum WasmFunctionName {
  ExpandTypes
  ExtractType
  ExtractProc
  ExtractStream
  Codegen
}

""" The input for the WASM function call """
type WasmInput {
  functionName: WasmFunctionName
  expandTypes?: ExpandTypesInput
  extractType?: ExtractTypeInput
  extractProc?: ExtractProcInput
  extractStream?: ExtractStreamInput
  codegen?: CodegenInput
}

""" The output for the WASM function call """
type WasmOutput {
  expandTypes?: ExpandTypesOutput
  extractType?: ExtractTypeOutput
  extractProc?: ExtractProcOutput
  extractStream?: ExtractStreamOutput
  codegen?: CodegenOutput
}

/***************
* EXPAND TYPES *
***************/

""" Input for expandTypes function """
type ExpandTypesInput {
  vdlSchema: string
}

""" Output for expandTypes function """
type ExpandTypesOutput {
  expandedSchema: string
}

/****************
* EXTRACT TYPES *
****************/

""" Input for extractType function """
type ExtractTypeInput {
  vdlSchema: string
  typeName: string
}

""" Output for extractType function """
type ExtractTypeOutput {
  typeSchema: string
}

""" Input for extractProc function """
type ExtractProcInput {
  vdlSchema: string
  rpcName: string
  procName: string
}

""" Output for extractProc function """
type ExtractProcOutput {
  procSchema: string
}

""" Input for extractStream function """
type ExtractStreamInput {
  vdlSchema: string
  rpcName: string
  streamName: string
}

""" Output for extractStream function """
type ExtractStreamOutput {
  streamSchema: string
}

/****************
* CODEGEN TYPES *
****************/

""" Represents a target for codegen function """
enum CodegenTarget {
  Go
  Typescript
  Dart
  Python
  OpenApi
  JsonSchema
}

""" Input for codegen function """
type CodegenInput {
  """ Schema to generate code from """
  vdlSchema: string

  """ Target to generate code for """
  target: CodegenTarget

  """ Configuration if the target is Go """
  goConfig?: {
    package: string
    genPatterns: bool
    genConsts: bool
    genClient: bool
    genServer: bool
  }

  """ Configuration if the target is Typescript """
  typescriptConfig?: {
    importExtension: string
    genPatterns: bool
    genConsts: bool
    genClient: bool
    genServer: bool
  }

  """ Configuration if the target is Dart """
  dartConfig?: {
    genPatterns: bool
    genConsts: bool
  }

  """ Configuration if the target is Python """
  pythonConfig?: {
    genPatterns: bool
    genConsts: bool
  }

  """ Configuration if the target is JsonSchema """
  jsonSchemaConfig?: {
    schemaId: string
  }

  """ Configuration if the target is OpenApi """
  openApiConfig?: {
    title: string
    version: string
    description?: string
    baseUrl?: string
    contactName?: string
    contactEmail?: string
    licenseName?: string
  }
}

""" Rrepresents a file generated by codegen function """
type CodegenFile {
  path: string
  content: string
}

""" Output for codegen function """
type CodegenOutput {
  """ The files generated for the picked code generator target """
  files: CodegenFile[]
}
